An Object Demo.

make person called (the-name) of age (years) :
	create
		name : the-name
		age : years

		(my) name :
			name

		(my) age :
			age

		give (me) the name (new-name) :
			name : new-name

		say hi to (me) :
			show {"Hello ", name, ". "}
			showline {"You are ", age, " years old."}

		make (me) (how-many) years older :
			age : how-many + age

		hi to (me) and farewell with (farewell) :
			say hi to (me)
			showline (farewell)

		same (me) as (another) :
			showline {"This person is ", name}
			all of {
				[name = (another) name],
				[age = (another) age]
			}

person : make person called "Amy" of age 52

showline ((person) name)
showline ((person) age)

say hi to (person)

nl

make (person) 3 years older
give (person) the name "David"
say hi to (person)

nl

another : make person called "Pauline" of age 37
yet-another : based on (person)
give (yet-another) the name "Stephen"

say hi to (another)
say hi to (person)
say hi to (yet-another)

nl

hi to (person) and farewell with "Time to go."
hi to (yet-another) and farewell with "See you later."

nl

showline (same (person) as (yet-another))
showline (same (yet-another) as (person))
give (yet-another) the name "David"
showline (same (person) as (yet-another))
showline (same (yet-another) as (person))
