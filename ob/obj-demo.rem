An Object Demo.

make person called (the-name) of age (years) :
	create
		name : the-name
		age : years

		say hi to () :
			show {"Hello ", name, ". "}
			showline {"You are ", age, " years old."}

		make () (how-many) years older :
			age : how-many + age

		hi to () and farewell with (farewell) :
			say hi to ()
			showline (farewell)

		same (another) as () :
			showline {"This person is ", name}
			all of {
				[name = another [name]],
				[age = another [age]]
			}

person : make person called "Amy" of age 52

showline (person [name])
showline (person [age])

say hi to (person)

nl

make (person) 3 years older
person [name] : "David"
say hi to (person)

nl

another : make person called "Pauline" of age 37
yet-another : based on (person) ; this still works
yet-another [name] : "Stephen"

say hi to (another)
say hi to (person)
say hi to (yet-another)

nl

hi to (person) and farewell with "Time to go."
hi to (yet-another) and farewell with "See you later."

nl

showline (same (person) as (yet-another))
showline (same (yet-another) as (person))
yet-another [name] : "David"
showline (same (person) as (yet-another))
showline (same (yet-another) as (person))
